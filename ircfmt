#!/bin/sed -f

# Naively breaks long lines in the input, without refilling. If a line
# contains an IRC nick, the nick is right-justified and any output lines
# broken out from the input line are indented to line up with the text
# following it. Timestamps (or anything else) can be placed before the
# nick, but they will only look good if they are all the same length.

# ----------------------------------------------------------------------

# Pad the nick (%13s), by adding the largest possible number of spaces
# and then eating up extra ones with a backtrack match.

s/\(<[^>]*>\)/           \1/
s/ *\([^ ]*.\{12\}>\)/ \1/

# Put an indent (possibly empty, if there is no nick) in the hold space,
# equal in length to the timestamp/nick/spaces that begin the line.

h
s/^\(.* <[^>]*> \)\?.*/\1/
s/./ /g
x

# While the final line of the pattern space is too long, append the
# indent, break the line with a greedy match, and bubble the indent up.

: wrap
/[^\n]\{78,\}$/ {
    G
    s/^\(.*\n\)\?\([^\n]\{20,78\}\) \([^\n]\+\)\(\n \+\)$/\1\2\4\3/
    t wrap
}
