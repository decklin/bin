#!/bin/sed -f

# Naively breaks long lines in the input, without refilling. If a line
# contains an IRC nick, the nick is right-justified and any output lines
# broken out from the input line are indented to line up with the text
# following it. Timestamps (or anything else) can be placed before the
# nick, but they will only look good if they are all the same length.

# ----------------------------------------------------------------------

# Pad the nick (%15s), by adding the largest possible number of spaces
# and then eating up extra ones with a backtrack match.

s/\(<[^>]*>\)/             \1/
s/ *\([^ ]*.\{15\}>\)/ \1/

# Put an indent in the hold space, long enough to line up with where the
# text starts on the first line.

h
s/^\(.* <[^>]*> \).*/\1/
s/./ /g
x

# While a line of the pattern space is too long, append the indent, break
# the line with a greedy match, and bubble the indent up.

: wrap
/[^\n]\{79,\}/ {
    G
    s/\([^\n]\{13,78\}\) \([^\n]*\)\(\n *\)$/\1\3\2/
    t wrap
}
